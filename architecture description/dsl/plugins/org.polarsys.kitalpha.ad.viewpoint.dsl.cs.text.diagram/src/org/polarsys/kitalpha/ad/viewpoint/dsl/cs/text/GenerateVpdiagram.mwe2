/*******************************************************************************
 * Copyright (c) 2014 Thales Global Services S.A.S.
 *  All rights reserved. This program and the accompanying materials
 *  are made available under the terms of the Eclipse Public License v1.0
 *  which accompanies this distribution, and is available at
 *  http://www.eclipse.org/legal/epl-v10.html
 * 
 * Contributors:
 *  Thales Global Services S.A.S - initial API and implementation
 ******************************************************************************/
module org.polarsys.kitalpha.ad.viewpoint.dsl.cs.text.GenerateVpdiagram

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.generator.*
import org.eclipse.xtext.ui.generator.*

var grammarURI = "classpath:/org/polarsys/kitalpha/ad/viewpoint/dsl/cs/text/Vpdiagram.xtext"
var file.extensions = "diagram.vptext"
var projectName = "org.polarsys.kitalpha.ad.viewpoint.dsl.cs.text.diagram"
var runtimeProject = "../${projectName}"

var polarsysCopyrightHeader = "/*******************************************************************************
 * Copyright (c) \${year} Thales Global Services S.A.S.
 *  All rights reserved. This program and the accompanying materials
 *  are made available under the terms of the Eclipse Public License v1.0
 *  which accompanies this distribution, and is available at
 *  http://www.eclipse.org/legal/epl-v10.html
 * 
 * Contributors:
 *  Thales Global Services S.A.S - initial API and implementation
 ******************************************************************************/"

Workflow {
    bean = StandaloneSetup {
            scanClassPath = true
            platformUri = "${runtimeProject}/.."
            
           	//Ecore
			uriMap = {
				from = "platform:/plugin/org.eclipse.emf.ecore/model/Ecore.ecore"
				to = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.ecore"
				}	
			uriMap = {	
				from = "platform:/plugin/org.eclipse.emf.ecore/model/Ecore.genmodel"
				to = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.genmodel"
				}
	
			//Vpspec
			uriMap = {
				from = "platform:/plugin/org.polarsys.kitalpha.ad.viewpoint.dsl.as.desc/model/vpdesc.ecore"
				to = "platform:/resource/org.polarsys.kitalpha.ad.viewpoint.dsl.as.desc/model/vpdesc.ecore"
				}	
			uriMap = {	
				from = "platform:/plugin/org.polarsys.kitalpha.ad.viewpoint.dsl.as.desc.model/model/vpdesc.genmodel"
				to = "platform:/resource/org.polarsys.kitalpha.ad.viewpoint.dsl.as.desc.model/model/vpdesc.genmodel"
				}
				
			//Vpdiagram
			uriMap = {
				from = "platform:/plugin/org.polarsys.kitalpha.ad.viewpoint.dsl.as.diagram/model/vpdiagram.ecore"
				to = "platform:/resource/org.polarsys.kitalpha.ad.viewpoint.dsl.as.diagram/model/vpdiagram.ecore"
				}	
			uriMap = {	
				from = "platform:/plugin/org.polarsys.kitalpha.ad.viewpoint.dsl.as.diagram.model/model/vpdiagram.genmodel"
				to = "platform:/resource/org.polarsys.kitalpha.ad.viewpoint.dsl.as.diagram.model/model/vpdiagram.genmodel"
				}
			
			//Vpdiagram Expressions
			uriMap = {
				from = "platform:/plugin/org.polarsys.kitalpha.ad.viewpoint.dsl.as.diagram.expression/model/expression.ecore"
				to = "platform:/resource/org.polarsys.kitalpha.ad.viewpoint.dsl.as.diagram.expression/model/expression.ecore"
				}	
			uriMap = {	
				from = "platform:/plugin/org.polarsys.kitalpha.ad.viewpoint.dsl.as.diagram.expression.model/model/expression.genmodel"
				to = "platform:/resource/org.polarsys.kitalpha.ad.viewpoint.dsl.as.diagram.expression.model/model/expression.genmodel"
				}
			
			//Common data
			uriMap = {
				from = "platform:/plugin/org.polarsys.kitalpha.ad.viewpoint.dsl.as.commondata/model/commondata.ecore"
				to = "platform:/resource/org.polarsys.kitalpha.ad.viewpoint.dsl.as.commondata/model/commondata.ecore"
				}	
			uriMap = {	
				from = "platform:/plugin/org.polarsys.kitalpha.ad.viewpoint.dsl.as.commondata.model/model/commondata.genmodel"
				to = "platform:/resource/org.polarsys.kitalpha.ad.viewpoint.dsl.as.commondata.model/model/commondata.genmodel"
				}
				
			//style customization
			uriMap = {
				from = "platform:/plugin/org.polarsys.kitalpha.ad.viewpoint.dsl.as.diagram.style.customization/model/vpstylecustomization.ecore"
				to = "platform:/resource/org.polarsys.kitalpha.ad.viewpoint.dsl.as.diagram.style.customization/model/vpstylecustomization.ecore"
				}	
			uriMap = {	
				from = "platform:/plugin/org.polarsys.kitalpha.ad.viewpoint.dsl.as.diagram.style.customization.model/models/vpstylecustomization.genmodel"
				to = "platform:/resource/org.polarsys.kitalpha.ad.viewpoint.dsl.as.diagram.style.customization.model/models/vpstylecustomization.genmodel"
				}
	
			
			//vpspec
			registerGeneratedEPackage = "org.polarsys.kitalpha.ad.viewpoint.dsl.as.model.vpdesc.VpdescPackage"	
			registerGenModelFile = "platform:/resource/org.polarsys.kitalpha.ad.viewpoint.dsl.as.desc.model/model/vpdesc.genmodel"
			
			//vpdiagram
			registerGeneratedEPackage = "org.polarsys.kitalpha.ad.viewpoint.dsl.as.model.vpdiagram.VpdiagramPackage"
			registerGenModelFile = "platform:/resource/org.polarsys.kitalpha.ad.viewpoint.dsl.as.diagram.model/model/vpdiagram.genmodel"
			
			//vpdiagram expressions
			registerGeneratedEPackage = "org.polarsys.kitalpha.ad.viewpoint.dsl.as.model.diagram.expression.ExpressionPackage"		
			registerGenModelFile = "platform:/resource/org.polarsys.kitalpha.ad.viewpoint.dsl.as.diagram.expression.model/model/expression.genmodel"
				
			//common data
			registerGeneratedEPackage = "org.polarsys.kitalpha.ad.viewpoint.dsl.as.model.commondata.CommondataPackage"
			registerGenModelFile = "platform:/resource/org.polarsys.kitalpha.ad.viewpoint.dsl.as.commondata.model/model/commondata.genmodel"
			
			registerGeneratedEPackage = "org.polarsys.kitalpha.ad.viewpoint.dsl.as.model.vpstylecustomization.VpstylecustomizationPackage"
			registerGenModelFile = "platform:/resource/org.polarsys.kitalpha.ad.viewpoint.dsl.as.diagram.style.customization.model/models/vpstylecustomization.genmodel"
			
			//sirius viewpoint
			registerGeneratedEPackage =	"org.eclipse.sirius.viewpoint.ViewpointPackage"
			//sirius description
			registerGeneratedEPackage =	"org.eclipse.sirius.viewpoint.description.DescriptionPackage"     
			//sirius diagram description
			registerGeneratedEPackage = "org.eclipse.sirius.diagram.description.DescriptionPackage"
			
			registerGeneratedEPackage = "org.eclipse.sirius.viewpoint.description.style.StylePackage"
			
			registerGeneratedEPackage = "org.eclipse.sirius.diagram.description.style.StylePackage"
        }
    
        component = DirectoryCleaner {
            directory = "${runtimeProject}/src-gen"
        }
    
        component = DirectoryCleaner {
            directory = "${runtimeProject}.ui/src-gen"
        }
    
        component = Generator {
        	fileHeader = polarsysCopyrightHeader
            pathRtProject = runtimeProject
            pathUiProject = "${runtimeProject}.ui"
        //    pathTestProject = "${runtimeProject}.tests"
            projectNameRt = projectName
            projectNameUi = "${projectName}.ui"
            language = {
                uri = grammarURI
                fileExtensions = file.extensions
    
                // Java API to access grammar elements (required by several other fragments)
                fragment = grammarAccess.GrammarAccessFragment {}
    
                // generates Java API for the generated EPackages
                fragment = ecore.EMFGeneratorFragment {}
    
                // Serializer 2.0
                fragment = serializer.SerializerFragment {
                	generateStub = false
                }
                
                // a custom ResourceFactory for use with EMF
                fragment = resourceFactory.ResourceFactoryFragment {
                    fileExtensions = file.extensions
                }
    
                // The antlr parser generator fragment.
                fragment = parser.antlr.XtextAntlrGeneratorFragment {
                //  options = {
                //      backtrack = true
                //  }
                }
    
                // java-based API for validation
                fragment = validation.JavaValidatorFragment {
                //    composedCheck = "org.eclipse.xtext.validation.ImportUriValidator"
                //    composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
                }
    
                // scoping and exporting API
                fragment = scoping.ImportNamespacesScopingFragment {}
                fragment = exporting.QualifiedNamesFragment {}
                fragment = builder.BuilderIntegrationFragment {}
    
                // generator API
                fragment = generator.GeneratorFragment {
                    generateMwe = false
                    generateJavaMain = false
                }
    
                // formatter API
                fragment = formatting.FormatterFragment {}
    
                // labeling API
                fragment = labeling.LabelProviderFragment {}
    
                // outline API
                fragment = outline.OutlineTreeProviderFragment {}
                fragment = outline.QuickOutlineFragment {}
    
                // quickfix API
                fragment = quickfix.QuickfixProviderFragment {}
    
                // content assist API
                fragment = contentAssist.JavaBasedContentAssistFragment {}
    
                // generates a more lightweight Antlr parser and lexer tailored for content assist
                fragment = parser.antlr.XtextAntlrUiGeneratorFragment {}
    
                // rename refactoring
                fragment = refactoring.RefactorElementNameFragment {}
    
                // provides the necessary bindings for java types integration
                fragment = types.TypesGeneratorFragment {}
    
                // provides a preference page for template proposals
                fragment = templates.CodetemplatesGeneratorFragment {}
    
                // provides a compare view
                fragment = compare.CompareFragment {
                     fileExtensions = file.extensions
                }
    
            }
        }
}

