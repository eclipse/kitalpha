/*******************************************************************************
 * Copyright (c) 2014 Thales Global Services S.A.S.
 *  All rights reserved. This program and the accompanying materials
 *  are made available under the terms of the Eclipse Public License v1.0
 *  which accompanies this distribution, and is available at
 *  http://www.eclipse.org/legal/epl-v10.html
 * 
 * Contributors:
 *  Thales Global Services S.A.S - initial API and implementation
 ******************************************************************************/
grammar org.polarsys.kitalpha.ad.viewpoint.dsl.cs.text.Activityexplorer with org.polarsys.kitalpha.ad.viewpoint.dsl.cs.text.Common


import "http://www.polarsys.org/kitalpha/ad/viewpoint/dsl/as/viewpointactivityexplorer/1.0.0"  as activityExplorer
import "http://www.eclipse.org/emf/2002/Ecore" as ecore


/**
 * @author: Faycal ABKA
 */

ViewpointActivityExplorer returns activityExplorer::ViewpointActivityExplorer:
	{activityExplorer::ViewpointActivityExplorer}
	'activity' 'explorer'
	name=FQN
	'{'
		('pages' '{'
			(ownedPages += Page)*
		'}')?
		(ownedPages += PageExtension)*
		(ownedSectionExtensions += SectionExtension)*
	'}'
;

AbstractPage returns activityExplorer::AbstractPage:
	Page			|
	PageExtension
;

Page returns activityExplorer::Page:
	{activityExplorer::Page}
	
	'page' name=STRING
	'{'
		('description' description=STRING)?
		('file' 'extension' fileExtensions = STRING)?
		('id' activityExplorerItemID=FQN)?
		'index' index=EInt
		
		(ownedOverview=Overview)?
		
		('header' '{'
			('image' 'on' ':' imagePathOn=STRING)?
			('image' 'off' ':' imagePathOff=STRING)?
		'}')?
		
		('tab' 'name' tabName=STRING)?
		('predicated' ':' hasPredicate=EBoolean)?
		('show' 'viewer' ':' showViewer=EBoolean)?
		
		('sections' '{'
			(ownedSections += Section)*
		'}')?
			
	'}'
;


Section returns activityExplorer::Section:
	{activityExplorer::Section}
	'section' name=STRING
	'{'
		('description' description=STRING)?
		('id' activityExplorerItemID=FQN)?
		'index' index = EInt
		('filtering' ':' filtering = EBoolean)?
		('expanded' ':' expanded = EBoolean)?
		('activities' '{'
			(ownedActivities += Activity)*
		'}')?
	'}'
;

Activity returns activityExplorer::Activity:
	{activityExplorer::Activity}
	'activity' name=STRING
	'{'
		('description' description=STRING)?
		('id' activityExplorerItemID=FQN)?
		'index' index=EInt
		('predicated' ':' hasPredicate=EBoolean)?
		('icon' ':' imagePathOff=STRING)?
	'}'
;



PageExtension returns activityExplorer::PageExtension:
	{activityExplorer::PageExtension}
	'page' 'extensions' 
	'{'
		'extended' 'page' extendedPageID=FQN
		(ownedSections+=Section)*
	'}'
;

SectionExtension returns activityExplorer::SectionExtension:
	{activityExplorer::SectionExtension}
	'section' 'extensions' '{'
		'extended' 'section' extendedSectionID=FQN
		(ownedActivities+=Activity)*
	'}'
;

Overview returns activityExplorer::Overview:
	{activityExplorer::Overview}
	'Overview'
	'{'
		('description' description=STRING)?
		('image' 'on'  ':' imagePathOn=STRING)?
		('image' 'off' ':' imagePathOff=STRING)?
	'}'
;
